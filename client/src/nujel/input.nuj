[def input/primary-did #f]
[def input/primary-did-count 0]

[def input/secondary-did #f]
[def input/secondary-did-count 0]

[def input/tertiary-did #f]
[def input/tertiary-did-count 0]

[def did-throw #f]
[def throw-start 0]

[defun player-primary! []
       "Make the player do a primary action"
       [def itemid [player-active-item-id]]
       [if [< itemid 256]
           [player-do-primary! input/primary-did-count]
           [when-not [item-primary! itemid input/primary-did-count]
                     [player-do-primary! input/primary-did-count]]]
       [set! input/primary-did #t]]

[defun player-secondary! []
       "Make the player do a secondary action"
       [def itemid [player-active-item-id]]
       [if [< itemid 256]
           [player-place-block! [player-active-slot]]
           [item-secondary! itemid input/secondary-did-count]]
       [set! input/secondary-did #t]]

[defun player-tertiary! []
       "Make the player do a tertiary action"
       [def itemid [player-active-item-id]]
       [when [>= itemid 256]
             [item-tertiary! itemid input/tertiary-did-count]]
       [set! input/tertiary-did #t]]

[defun player-throw! []
       "Try to throw the currently held item"
       [when [zero? throw-start] [set! throw-start [time/milliseconds]]]
       [when [> [- [time/milliseconds] throw-start] 500]
             [let [[zoom [+ 1.0 [min 1.0 [/ [- [time/milliseconds] throw-start 500] 1000.0]]]]]
                  [player-zoom! zoom]]]
       [set! did-throw #t]]

[defun player-do-throw! [timeheld]
       [if [<= [- [time/milliseconds] throw-start] 500]
           [player-drop-item! [player-active-slot]]
           [throw-item throw-spin [* [player-zoom] 0.07]]]
       [set! throw-start 0]
       [player-zoom! 1.0]]

[defun input-tick []
       "Evals all Input handlers in order"
       [input-mouse-tick]
       [input-keyboard-tick]


       [++ input/primary-did-count]
       [when-not input/primary-did
                 [player-stop-mining!]
                 [set! input/primary-did-count 0]]
       [set! input/primary-did #f]

       [++ input/secondary-did-count]
       [when-not input/secondary-did
                 [set! input/secondary-did-count 0]]
       [set! input/secondary-did #f]

       [++ input/tertiary-did-count]
       [when-not input/tertiary-did
                 [set! input/tertiary-did-count 0]]
       [set! input/tertiary-did #f]


       [when [and [not did-throw]
                  [!= throw-start 0]]
             [player-do-throw! [- [time/milliseconds] throw-start]]]
       [set! did-throw #f]]
