[def hook/shot/timeout 0]
[def hook/rope #nil]
[def hook/ropes [array/allocate 32]]
[def hook/max-length 96.0]
[def hook/min-length  2.0]
[def hook/winch-speed 0.2]

[defun hook/valid? []
  [if [rope/valid? hook/rope]
      #t
      [do [player/dont-move! #f]
          [set! hook/rope #nil]
          #f]]]

[defun hook/extend! []
  [when [hook/valid?]
    [rope/length! hook/rope [min [+ [rope/length hook/rope] hook/winch-speed]
                                 hook/max-length]]]]

[defun hook/pull-in! []
  [when [hook/valid?]
    [rope/length! hook/rope [max [- [rope/length hook/rope] hook/winch-speed]
                                 hook/min-length]]]]

[defun hook/shoot! []
  [player/close-glider!]
  [when [> [time/milliseconds] hook/shot/timeout]
    [set! hook/shot/timeout [+ 200 [time/milliseconds]]]
    [message/send 0 [list :hook-shoot [player-pos] [player-rot]]]]]

[defhandler :hook-rope [client-id msg]
  [def player [cadr msg]]
  [def ent [caddr msg]]
  [def sender [cadddr msg]]
  [def rope [-> [rope/new* player ent]
                [rope/length! 192]]]
  [array/set! hook/ropes sender rope]
  [when [zero? sender]
    [set! hook/rope rope]]]

[defun hook/tick []
  [try display/error
       [and hook/rope
            [hook/valid?]
            [> [rope/distance hook/rope] hook/max-length]
            [message/send 0 [list :hook-clear]]
            [do [player/dont-move! #f]
                [set! hook/rope #nil]]]]]

[defhandler :hook-hit [client-id msg]
            [when hook/rope
              [player/dont-move! #t]
              [rope/length! hook/rope [min hook/max-length [+ 8 [rope/distance hook/rope]]]]]]

[defhandler :hook-clear [client-id msg]
            [player/dont-move! #f]
            [array/set! hook/ropes [cadr msg] #nil]]
